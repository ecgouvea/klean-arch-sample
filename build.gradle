group 'br.com.helpdev'
version '0.0.1'

description = "Clean Architecture with Kotlin and Gradle"

buildscript {
    ext.kotlin_version = '1.3.41'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    ext {
        versions = [
                junit  : "5.5.1",
                assertj: "3.11.1",
                spring : "2.1.7.RELEASE"
        ]

        libs = [
                unit_test: [
                        "org.assertj:assertj-core:${versions.assertj}",
                        "org.junit.jupiter:junit-jupiter:${versions.junit}",
                        "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
                        "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
                ],
                spring   : [
                        "org.springframework.boot:spring-boot-starter-web:${versions.spring}",
                        "org.springframework.boot:spring-boot-starter-logging:${versions.spring}",
                ],
        ]
    }
}

configure(allprojects - project(":application")) {
    apply plugin: "kotlin"
    apply plugin: 'java'

    compileKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
            javaParameters = true
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }
}